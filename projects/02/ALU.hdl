// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:

    // 1st + 2nd iteration
    Mux16(a = x, b = false, sel = zx, out = x1);
    Not16(in = x1, out = xNot);
    Mux16(a = x1, b = xNot, sel = nx, out = x2);

    // 3rd + 4th  iteration
    Mux16(a = y, b = false, sel = zy, out = y1);
    Not16(in = y1, out = yNot);
    Mux16(a = y1, b = yNot, sel = ny, out = y2);

    // doing arithmetic operations
    And16(a = x2, b = y2, out = xAndy);
    Add16(a = x2, b = y2, out = xAddy);
    
    // placement according to f
    Mux16(a = xAndy, b = xAddy, sel = f, out = xFy);

    // placement according to no
    Not16(in = xFy, out = xFyNot);
    Mux16(a = xFy, b = xFyNot, sel = no, out = out,
                out[0..7] = tmp1, out[8..15] = tmp2, out[15] = ng); // placenent ng value
    
    // placement in zr value
    Or8Way(in = tmp1, out = or1);
    Or8Way(in = tmp2, out = or2);
    Or(a = or1, b = or2, out = zrNot);
    Not(in = zrNot, out = zr);
}